import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:3.0.0"
        classpath "io.freefair.gradle:maven-plugin:3.0.0"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.9"
        classpath "com.netflix.nebula:gradle-lint-plugin:10.4.2"
        classpath "de.aaschmid:gradle-cpd-plugin:1.1"
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.0"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.20.0"
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.1.1"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2"
    }
}

version = resolveVersion()

allprojects {
    group = 'org.joinfaces'
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency 'javax.faces:javax.faces-api:2.3'

            dependency('org.glassfish:javax.faces:2.3.8')

            dependencySet('org.apache.myfaces.core:2.3.2') {
                entry "myfaces-api"
                entry("myfaces-impl") {
                    exclude "commons-logging:commons-logging"
                }
            }

            dependency('javax.enterprise:cdi-api:2.0.SP1') {
                exclude "javax.el:javax.el-api"
            }

            dependency "org.apache.tomcat:tomcat-jasper:${importedProperties['tomcat.version']}"
            dependency 'org.jboss.spec.javax.servlet.jsp:jboss-jsp-api_2.3_spec:1.0.3.Final'

            dependency 'io.github.classgraph:classgraph:4.6.13'

            dependency 'org.primefaces:primefaces:6.2'
            dependencySet("org.primefaces.extensions:6.2.10") {
                entry "primefaces-extensions"
                entry "resources-ckeditor"
            }

            dependency 'net.bootsfaces:bootsfaces:1.4.1'
            dependency('de.beyondjava:angularFaces-core:2.1.12') {
                exclude "commons-logging:commons-logging"
            }
            dependency 'org.butterfaces:components:3.0.7'

            dependencySet('org.richfaces:4.5.17.Final') {
                entry "richfaces"
                entry "richfaces-a4j"
            }

            dependencySet('org.icefaces:4.3.0') {
                entry 'icefaces'
                entry 'icefaces-ace'
            }

            dependency "commons-fileupload:commons-fileupload:1.4"

            dependency('org.jboss.weld.servlet:weld-servlet-core:3.0.5.Final') {
                exclude 'org.jboss.spec.javax.el:jboss-el-api_3.0_spec'
                exclude 'org.jboss.spec.javax.interceptor:jboss-interceptors-api_1.2_spec'
            }

            dependencySet("org.ocpsoft.rewrite:3.4.2.Final") {
                entry "rewrite-servlet"
                entry "rewrite-config-prettyfaces"
                entry "rewrite-integration-spring"
                entry "rewrite-integration-faces"
                entry "rewrite-integration-cdi"
            }
            dependency 'org.ocpsoft.logging:logging-adapter-slf4j:1.0.5.Final'

            dependencySet('com.github.adminfaces:1.0.0-RC20') {
                entry "admin-template"
                entry "admin-theme"
            }

            dependency 'javax.ejb:javax.ejb-api:3.2.2'
        }
    }

    apply plugin: 'nebula.lint'

    gradleLint {
        rules = ['transitive-duplicate-dependency-class']
        criticalRules = ['duplicate-dependency-class', 'dependency-parentheses']
    }

    plugins.withId("java") {
        apply plugin: "io.freefair.lombok"

        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }

        javadoc.failOnError = false

        tasks.withType(Test) {
            useJUnitPlatform()
        }

        afterEvaluate {
            jar.manifest {
                attributes 'Implementation-Title': "$description",
                        'Implementation-Version': "$version",
                        'Implementation-Vendor': "JoinFaces"
            }
        }

        tasks.withType(JDepend) { jDepend ->
            jDepend.group = "verification"
            tasks.create("${jDepend.name}Cycles") {
                dependsOn jDepend
                check.dependsOn it
                group = "verification"
                inputs.file { jDepend.reports.xml.destination }
                doLast {
                    def reportFile = jDepend.reports.xml.destination
                    def numberOfCycles = new XmlSlurper().parse(reportFile).Cycles.Package.size()
                    assert numberOfCycles == 0, "We have detected $numberOfCyclesMain cycles. Checkout report file: $fileMain."
                }
            }
        }
    }

    plugins.withId("checkstyle") {
        checkstyle {
            toolVersion = '8.12'
        }
    }

    plugins.withId("pmd") {
        pmd {
            ruleSetConfig = resources.text.fromFile("$rootDir/config/pmd/pmd-ruleset.xml")
            ruleSets = []
        }
        pmdTest.enabled = false
    }

    plugins.withId("jacoco") {
        jacocoTestReport.reports.xml.enabled true
        jacoco.toolVersion = "0.8.2"
    }

    plugins.withId("maven-publish") {
        project.apply plugin: 'de.marcphilipp.nexus-publish'
        project.apply plugin: "signing"
        project.apply plugin: "io.freefair.maven-central.validate-poms"

        signing {
            required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("publish") }
        }

        publishing {
            publications.withType(MavenPublication) {
                pom {
                    url = 'http://joinfaces.org'
                    name = provider { project.description }
                    description = provider { project.description }
                    inceptionYear = '2016'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    organization {
                        name = 'JoinFaces'
                        url = 'http://joinfaces.org'
                    }
                    developers {
                        developer {
                            id = 'persapiens'
                            name = 'Marcelo Romulo Fernandes'
                            email = 'persapiens@gmail.com'
                            timezone = '-3'
                        }
                        developer {
                            id = 'larsgrefer'
                            name = 'Lars Grefer'
                            email = 'github@larsgrefer.de'
                            timezone = 'Europe/Berlin'
                        }
                    }
                    ciManagement {
                        system = 'Travis CI'
                        url = 'https://travis-ci.org/joinfaces/joinfaces/'
                    }
                    issueManagement {
                        system = 'Github'
                        url = 'https://github.com/joinfaces/joinfaces/issues'
                    }
                    scm {
                        connection = 'scm:git:https://github.com/joinfaces/joinfaces.git'
                        developerConnection = 'scm:git:git@github.com:joinfaces/joinfaces.git'
                        url = 'https://github.com/joinfaces/joinfaces/'
                    }
                }
            }
        }
    }
}

apply plugin: "de.aaschmid.cpd"

cpdCheck {
    exclude "**/*Test.java"
    exclude "**/*IT.java"
}

apply plugin: "maven-publish"
apply plugin: "io.freefair.aggregate-javadoc-jar"

aggregateJavadoc.failOnError = false
aggregateJavadoc.title = "JoinFaces $version API"
aggregateJavadoc.options.links "https://docs.oracle.com/javase/10/docs/api/"
aggregateJavadoc.options.links "https://docs.spring.io/spring-boot/docs/${org.springframework.boot.gradle.plugin.SpringBootPlugin.SPRING_BOOT_VERSION}/api/"

apply plugin: "io.codearte.nexus-staging"

nexusStaging {
    stagingProfileId = '9d2198adfd84d'
    username = findProperty('ossrhUser')
    password = findProperty('ossrhPass')
}

apply plugin: "org.asciidoctor.convert"

task propertyDocs(type: org.joinfaces.build.PropertyDocumentation) {
    inputFile = file("joinfaces-autoconfigure/build/classes/java/main/META-INF/spring-configuration-metadata.json")
    outputFile = file("src/docs/asciidoc/generated/_properties.adoc")
    dependsOn ":joinfaces-autoconfigure:classes"
}

task bomDocs(type: org.joinfaces.build.BomDocumentation) {
    afterEvaluate {
        dependsOn generatePomFileForBomPublication
        inputFile = generatePomFileForBomPublication.destination
    }
    outputFile = file("src/docs/asciidoc/generated/_versions.adoc")
}

asciidoctor {
    backends = ['html5']
    dependsOn propertyDocs, bomDocs
    attributes revnumber: project.version,
            'spring-boot-version': org.springframework.boot.gradle.plugin.SpringBootPlugin.SPRING_BOOT_VERSION
}

task docsZip(type: Zip) {
    group = 'build'
    classifier = 'docs'
    dependsOn asciidoctor

    into("api") {
        from aggregateJavadoc
    }
    from ("$asciidoctor.outputDir/html5") {
        into "reference"
    }
}

task uploadDocs(type: org.joinfaces.build.UploadDocumentation) {
    inputFile = docsZip.archivePath
    dependsOn docsZip
    username = findProperty("joinfacesDocsUser")
    password = findProperty("joinfacesDocsPass")
}

publishing {
    publications {
        bom(MavenPublication) {
            artifactId = 'joinfaces-dependencies'

            pom {
                name = 'JoinFaces Dependencies'
                description = 'JoinFaces Dependencies'
            }

            pom.withXml {
                rootProject.subprojects { p ->
                    if (p.pluginManager.hasPlugin('maven-publish')) {
                        def depNode = asNode().dependencyManagement.first().get('dependencies').first().appendNode('dependency')
                        depNode.appendNode('groupId', p.group)
                        depNode.appendNode('artifactId', p.name)
                        depNode.appendNode('version', p.version)
                    }
                }
            }

            signing.sign it
        }
        javadoc(MavenPublication) {
            artifactId = 'joinfaces'

            pom {
                name = "JoinFaces"
                description = "JoinFaces"
            }

            artifact aggregateJavadocJar

            signing.sign it
        }
    }
}

def resolveVersion() {
    if (project.version != "unspecified") {
        logger.lifecycle("Using explicit version $project.version")
        return project.version
    }
    def travisTag = System.getenv('TRAVIS_TAG')
    if (travisTag) {
        logger.lifecycle("Using TRAVIS_TAG as version: $travisTag")
        return travisTag
    }
    def travisBranch = System.getenv('TRAVIS_BRANCH')
    if (travisBranch) {
        travisBranch = travisBranch.replace("/", "-")
        logger.lifecycle("Using TRAVIS_BRANCH as version: $travisBranch-SNAPSHOT")
        return "$travisBranch-SNAPSHOT"
    }
    try {
        def gitBranch = "git symbolic-ref --short HEAD".execute().text.trim()
        if (gitBranch) {
            gitBranch = gitBranch.replace("/", "-")
            logger.lifecycle("Using git branch as version: $gitBranch-SNAPSHOT")
            return "$gitBranch-SNAPSHOT"
        }
    } catch (Exception ignored) {
    }
    return project.version
}
