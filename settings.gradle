plugins {
    id "io.freefair.settings.plugin-versions" version "8.3"
    id "com.gradle.enterprise" version "3.14.1"
}

rootProject.name = 'joinfaces-parent'

boolean isCiServer = System.getenv().containsKey("CI")

gradleEnterprise {
    if (isCiServer) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

buildCache {
    remote(HttpBuildCache) {
        url = 'https://build-cache.grefer-hosting.de/cache/'
        push = isCiServer
        credentials {
            username = "joinfaces"
            password = providers.gradleProperty('joinfacesBuildCachePassword').getOrElse(null)
        }
    }
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven { url 'https://repository.apache.org/content/repositories/snapshots' }
        maven { url 'https://repo.spring.io/milestone' }
    }
}

include ':joinfaces'
include ':joinfaces-dependencies'
include ':joinfaces-documentation'

include ':joinfaces-tools:joinfaces-scan-tools'
include ':joinfaces-tools:joinfaces-gradle-plugin'
include ':joinfaces-tools:joinfaces-maven-plugin'

include ':joinfaces-test'
include ':joinfaces-security-taglib'
include ':joinfaces-autoconfigure'
include ':joinfaces-starters:mojarra-spring-boot-starter'
include ':joinfaces-starters:mojarra3-spring-boot-starter'
include ':joinfaces-starters:mojarra4-spring-boot-starter'
include ':joinfaces-starters:myfaces-spring-boot-starter'
include ':joinfaces-starters:myfaces3-spring-boot-starter'
include ':joinfaces-starters:myfaces4-spring-boot-starter'
include ':joinfaces-starters:omnifaces-spring-boot-starter'
include ':joinfaces-starters:jsf-spring-boot-starter'
include ':joinfaces-starters:faces-spring-boot-starter'
include ':joinfaces-starters:security-spring-boot-starter'
include ':joinfaces-starters:primefaces-spring-boot-starter'
include ':joinfaces-starters:weld-spring-boot-starter'
include ':joinfaces-starters:adminfaces-spring-boot-starter'
include ':joinfaces-starters:tobago-spring-boot-starter'
include ':joinfaces-starters:rewrite-spring-boot-starter'
include ':joinfaces-starters'

include ':test-projects:tomcat-mojarra3'
include ':test-projects:tomcat-mojarra4'
include ':test-projects:tomcat-myfaces3'
include ':test-projects:tomcat-myfaces4'
include ':test-projects:jetty-mojarra'
include ':test-projects:jetty-myfaces'
include ':test-projects:undertow-mojarra'
include ':test-projects:undertow-myfaces'
