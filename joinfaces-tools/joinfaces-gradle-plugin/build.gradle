import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'com.gradle.plugin-publish' version '1.0.0'
}

description = "JoinFaces Gradle Plugin"

gradlePlugin {
    plugins {
        joinfaces {
            id = 'org.joinfaces'
            implementationClass = 'org.joinfaces.gradle.JoinfacesPlugin'
            displayName = 'JoinFaces Gradle plugin'
            description = 'JoinFaces Gradle Plugin'
        }
        classpathScan {
            id = 'org.joinfaces.classpath-scan'
            implementationClass = 'org.joinfaces.gradle.ClasspathScanPlugin'
            displayName = 'JoinFaces classpath-scan plugin'
            description = 'Performs the classpath scan at build-time for a faster application startup.'
        }
        bom {
            id = 'org.joinfaces.dependency-management'
            implementationClass = 'org.joinfaces.gradle.BomPlugin'
            displayName = 'JoinFaces dependency management plugin'
            description = 'Adds the joinfaces-dependencies bom to the projects dependency management.'
        }
    }
}

pluginBundle {
    website = 'https://github.com/joinfaces/joinfaces'
    vcsUrl = 'https://github.com/joinfaces/joinfaces'
    tags = ['joinfaces', 'jsf']
}

dependencies {
    api project(":joinfaces-tools:joinfaces-scan-tools")

    api "org.springframework.boot:spring-boot-gradle-plugin:${SpringBootPlugin.SPRING_BOOT_VERSION}"
    api "io.spring.gradle:dependency-management-plugin"
}
