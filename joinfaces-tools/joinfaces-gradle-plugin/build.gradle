import org.springframework.boot.gradle.plugin.SpringBootPlugin

apply plugin: "java-gradle-plugin"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "maven-publish"
apply plugin: "io.freefair.maven-jars"

description = "JoinFaces Gradle Plugin"

gradlePlugin {
    plugins {
        joinfaces {
            id = 'org.joinfaces'
            implementationClass = 'org.joinfaces.gradle.JoinfacesPlugin'
        }
        classpathScan {
            id = 'org.joinfaces.classpath-scan'
            implementationClass = 'org.joinfaces.gradle.ClasspathScanPlugin'
        }
        bom {
            id = 'org.joinfaces.dependency-management'
            implementationClass = 'org.joinfaces.gradle.BomPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/joinfaces/joinfaces'
    vcsUrl = 'https://github.com/joinfaces/joinfaces'
    tags = ['joinfaces', 'jsf']

    plugins {
        joinfaces {
            displayName = 'JoinFaces Gradle plugin'
            description = 'JoinFaces Gradle Plugin'
        }
        classpathScan {
            displayName = 'JoinFaces classpath-scan plugin'
            description = 'Performs the classpath scan at build-time for a faster application startup.'
        }
        bom {
            displayName = 'JoinFaces dependency management plugin'
            description = 'Adds the joinfaces-dependencies bom to the projects dependency management.'
        }
    }
}

dependencies {
    compile project(":joinfaces-tools:joinfaces-scan-tools")

    compile "org.springframework.boot:spring-boot-gradle-plugin:${SpringBootPlugin.SPRING_BOOT_VERSION}"
    compile "io.spring.gradle:dependency-management-plugin"
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

publishing {
    publications.withType(MavenPublication) {
        signing.sign it
    }
}

afterEvaluate {
    pluginBundle.plugins.each { plugin ->
        publishing.publications.getByName("${plugin.name}PluginMarkerMaven") { publication ->
            publication.pom.name = plugin.displayName
            publication.pom.description = plugin.description
        }
    }
}
