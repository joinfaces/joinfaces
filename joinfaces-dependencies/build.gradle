import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-platform'
    id 'maven-publish'
}

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform(SpringBootPlugin.BOM_COORDINATES)

    constraints {
        //Mojarra
        api 'jakarta.faces:jakarta.faces-api:4.0.1'

        api 'io.github.classgraph:classgraph:4.8.165'

        //Jakarta EE (if not covered by Spring Boot)
        api 'jakarta.enterprise:jakarta.enterprise.cdi-api:4.0.1'
        api 'jakarta.ejb:jakarta.ejb-api:4.0.1'

        //AdminFaces
        api "com.github.adminfaces:admin-template:2.0"
        api "com.github.adminfaces:admin-theme:1.6.0"

        //Primefaces
        api 'org.primefaces:primefaces:13.0.3'
        api "org.primefaces.extensions:primefaces-extensions:13.0.3"
        api "org.primefaces.extensions:resources-monacoeditor:13.0.3"

        api 'org.jboss.weld.servlet:weld-servlet-core:5.1.2.Final'

        api 'org.apache.openwebbeans:openwebbeans-web:4.0.1'
        api 'org.apache.openwebbeans:openwebbeans-jsf:4.0.1'
        api 'org.apache.openwebbeans:openwebbeans-el22:4.0.1'

        //MyFaces Tobago
        api 'org.apache.myfaces.tobago:tobago-core:6.0.0'
        api 'org.apache.myfaces.tobago:tobago-theme-standard:6.0.0'
        api 'org.apache.myfaces.tobago:tobago-theme-speyside:6.0.0'
        api 'org.apache.myfaces.tobago:tobago-theme-scarborough:6.0.0'
        api 'org.apache.myfaces.tobago:tobago-theme-roxborough:6.1.0'
        api 'org.apache.myfaces.tobago:tobago-theme-charlotteville:6.0.0'

        api 'org.omnifaces:omnifaces:4.3'

        //Ocpsoft Rewrite
        api 'org.ocpsoft.rewrite:rewrite-servlet:9.0.2.Final'
        api 'org.ocpsoft.rewrite:rewrite-config-prettyfaces:9.0.2.Final'
        api 'org.ocpsoft.rewrite:rewrite-integration-spring:9.0.2.Final'
        api 'org.ocpsoft.rewrite:rewrite-integration-faces:9.0.2.Final'
        api 'org.ocpsoft.rewrite:rewrite-integration-cdi:9.0.2.Final'
        
        //JoinFaces
        api project(":joinfaces")
        api project(":joinfaces-autoconfigure")
        api project(":joinfaces-security-taglib")
        project(":joinfaces-starters").subprojects { starter ->
            api starter
        }
        project(":joinfaces-tools").subprojects { tool ->
            api tool
        }
    }
}

publishing {
    publications {
        bom(MavenPublication) {
            from components.javaPlatform

            pom {
                name = 'JoinFaces Dependencies'
                description = 'JoinFaces Dependencies'
            }

            signing.sign it
        }
    }
}


